= Types

:icons: font

== Syntax of Types

[source,java]
----
/* type constructor */
tycon ::= QUALIFIER? CONID 
        | '[' ']'            /* list type constructor */
        | '(' ')'            /* unit type constructor */
        | '(' ','+ ')'       /* tuple type constructors */
        | '(' '->' ')'       /* function type constructor */

/* simple type variable */
simpleTyvar ::= VARID

/* type variable with kind */
tyvar ::= simpleTyvar
        | '(' simpleTyvar '::' kind ')'
        | genericTypeVar

/* bounded type variables, needed in native definitions */
genericTypeVar ::=
          '(' simpleTyvar '≤' typeApp ')'   /* T extends C<...> */

/* wild card types, needed in native definitions */
wildCardType:
          '('  '≤' typeApp ')'              /* ? extends C<...> */
        | '('  '≥' typeApp ')'              /* ? super   C<...> */

/* type kind */
simplekind ::= '*' 
        | '(' kind ')'

kind ::= simplekind
        | simplekind '->' kind
----

For a discussion of kinds, <<kinds.adoc#_kinds,look here>>.

Type variables normally scope over a single type expression, where
two type variables with the same name stand for the same type.

In `data`, `newtype` or `type` definitions, type variables introduced left from the equals sign scope over the whole right hand side.

In type `class` definitions, there is one type variable that stands 
for the instantiated type and scopes over all definitions in
the where block of the class definition.

In generic type variables, the given type denotes the _upper bound_ for some native type. It must be an instantiation of a native type or a tuple of such types. 

.Given
 data Enum (e ≤ Enum e) = pure native java.lang.Enum
 data Comparable t = pure native java.lang.Comparable
 
the type variable
 
 t ≤ (Enum t, Comparable t)
 
will be introduced in corresponding java constructs like

  T extends java.lang.Enum<T> & java.lang.Comparable<T>

In wild card types, the given type denotes the _upper bound_ or the _lower bound_ of some native type. Here, no tuples are allowed since wild card types in Java can name a single type only.

[source,java]
----
/* type term */
typeTerm ::= tyvar 
          | tycon 
          | wildCardType               /* for native types */
          | '(' quantifiedType ')'     /* type in parentheses */
          | '(' type (',' type)+ ')'   /* tuple type */
          | '[' type ']'               /* list type */

/* type application */
typeApp ::= typeTerm+

/* type */
type ::= typeApp ( '->' type)         /* function type */
constrainedType ::= constraints '=>' type 
constraint ::= QUALIFIER? CONID typeApp
constraints ::= constraint | '(' constraint (',' constraint)* ')'
quantifiedType ::= ('forall' | '∀') tyvar+ '.' (constrainedType | type)
----

.Examples
 String             -- the String type
 Int                -- the Int type
 M.Foo              -- the type Foo from name space M
 t                  -- a type variable, stands for a type
 (t :: * -> *)      -- a type variable with kind * -> *
 ()                 -- the unit type
 [Int]              -- list of integers
 [] Int             -- the same
 (a,a,b)            -- a 3 tuple where the first 2 elements have the same type
 (Int, [String])    -- a tuple, holding an Int and a list of Strings
 [(Int, [String])]  -- a list of such tuples
 Int -> Int         -- a function from Int to Int
 (a->b) -> [a] -> [b] -- a function that takes another function
                      -- from a to b and a list of a and produces
                      -- a list of b
 forall a b . (a->b) -> [a] -> [b]
                      -- same as before, but explicitly quantified
 (∀ t.[t]->[t]) -> [a] -> [b] -> ([a], [b])
 
Types can mostly be written without explicit quantification. In this case we say the type is implicitly quantified. A `forall` with all free type variables is supplied by the compiler.

However, for higher rank types we need an explicit `forall`. The last type in the examples above shows this. We have here a function that takes a higher order function that works on all lists, regardless of the element type. For example, `reverse` is such a function. This makes it possible for our higher rank function to apply the argument function to two lists of different types. 


== Standard Types

|===
| Frege type | Java type | Explanation

| Bool | boolean |
| Int | int |
| Long | long |
| Integer | java.math.BigInteger |
| Float | float |
| Double | double |
| Char | char |
| String | java.lang.String |
| Regex | java.util.regex.Pattern |
| () | | the unit type with the element ()
| [a] | | lists
| (a,b) | | tuples (up to 26 elements)
| a -> b | | functions
| Maybe a | | optional value
| Either a b | | generic sum type

|===